/********************************************************************************
 * Copyright 2017 Capital One Services, LLC and Bitwise, Inc.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/
apply from: '../version.gradle'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
group = 'hydrograph'
archivesBaseName = 'hydrograph.engine.spark'

uploadArchives {
    repositories {
        mavenLocal()
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (testJar) {
                classifier = 'tests'
            }
            artifact (sourcesJar) {
                classifier = 'sources'
            }

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

test {
    exclude '**/SparkUnionAllComponentTest*'
    exclude '**/InputMysqlComponentTest*'
    exclude '**/InputFileParquetComponentTest*'
    exclude '**/OutputFileParquetComponentTest*'
    exclude '**/InputTeradataComponentTest*'
    exclude '**/HiveParquetInputTest*'
    exclude '**/HiveParquetOutputTest*'
    exclude '**/HiveTextInputTest*'
    exclude '**/HiveTextOutputTest*'
    exclude '**/RunSQLComponentTest*'
}

//  version = null //Version in jar name set to null to keep the name consistent for travis.yml to ftp the jar 


repositories {
	flatDir {
       dirs '../libs'
    }
	mavenLocal()
    mavenCentral()
    maven {
        name 'redshift'
        url 'http://redshift-maven-repository.s3-website-us-east-1.amazonaws.com/release'
    }

}

sourceSets{
    main.scala.srcDir 'src/main/scala'
    test.scala.srcDir 'src/test/scala'
    main.resources.srcDir 'src/main/resources'
}


configurations {
    compile.exclude group: 'commons-beanutils', module: 'commons-beanutils'
}

dependencies {
	compile (group: 'org.apache.spark', name: 'spark-core_2.11', version: '2.0.2')
    compile group: 'org.apache.spark', name: 'spark-sql_2.11', version: '2.0.2'
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.8'
    compile group: 'org.apache.spark', name: 'spark-hive_2.11', version: '2.0.2'
    compile group: 'org.apache.hadoop', name: 'hadoop-aws', version: '2.7.2'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.34'

    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.4'

    compileOnly name: 'teradata-tdgssconfig-0.1'
    compileOnly name: 'teradata-terajdbc4-0.1'
    compile group: 'org.fluttercode.datafactory', name: 'datafactory', version: '0.8'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile project(':hydrograph.engine.core')
    compile project(':hydrograph.engine.transformation')
    compile project(':hydrograph.engine.expression')
    compileOnly name: 'oracle-ojdbc6-11.2.0.4'
    compile group: 'com.ibatis', name: 'ibatis2-common', version: '2.1.7.597'
    compile group: 'com.amazon.redshift', name: 'redshift-jdbc42', version: '1.2.1.1001'

    compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.188'

    compile group: 'com.databricks', name: 'spark-redshift_2.11', version: '2.0.1'
    //compile group: 'org.apache.hadoop', name: 'hadoop-core', version: '0.20.2'

    compile group:'com.jcraft',name:'jsch',version:'0.1.54'
    compile group:'commons-net',name:'commons-net',version:'3.3'

    compile group: 'org.apache.poi', name: 'poi', version: '3.15'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.15'
    compile group: 'org.apache.xmlbeans', name: 'xmlbeans', version: '2.6.0'
}
